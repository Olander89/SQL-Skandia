USE [MART]
GO

/****** Object:  StoredProcedure [ssrs].[usp_Ekonomi_IFRS9_Osäkra_Krediter_Blancoanalys]    Script Date: 2020-11-04 16:23:30 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO








CREATE PROCEDURE [ssrs].[usp_Ekonomi_IFRS9_Osäkra_Krediter_Blancoanalys]
@Period				VARCHAR(6),
@UCRP_Kontokredit	DECIMAL(6,4),
@UCRP_Kreditkort	DECIMAL(6,4),
@UCRP_Privatlan		DECIMAL(6,4),  
@UCRP_Övrigt		DECIMAL(6,4),
@CCF_Kontokredit	DECIMAL(4,2),
@CCF_Kreditkort		DECIMAL(4,2), 
@CCF_Privatlan		DECIMAL(4,2),  
@CCF_Övrigt			DECIMAL(4,2),
@LGD				DECIMAL(4,2)	 

 
AS

/*****************************************************************************************************
Beskrivning : Beställare: Alvaro Cooper
Kommentar   : 
Exempel     : 
******************************************************************************************************
Skapad av   : Fredrik Olander
Datum       : 2020-09-21
******************************************************************************************************/

IF OBJECT_ID('tempdb..#rapporttabell') IS NOT NULL DROP TABLE #rapporttabell
--DECLARE @Period VARCHAR(6) = '201811'

DECLARE @RapportPeriod DATE
SET @RapportPeriod = DATEADD(D,-1,DATEADD(M,1,@Period + '01'))

DECLARE @Användare varchar(50) = system_user

--Privatlån
SELECT 
	[KontoID]							= lan.LåneKontoID,
    [Typ av lån]					    = CASE 
											WHEN kredtyp.KreditTyp = 'Privatlån' THEN 'Privatlån'
											ELSE 'Övrigt'
										  END,
    [KreditTyp]							= kredtyp.KreditTyp,	
    [Kontonummer]					    = konto.LåneKontonummer,
    [Personnummer]					    = konto.Personnummer,
    [Skuld]								= lan.AktuellSkuld,
    [Ränta]								= ISNULL(fk.Ränta,0),
    [Skuld+Ränta]					    = (lan.AktuellSkuld + ISNULL(fk.Ränta,0)),
    [Kundens totala exponering]		    = CAST(0.00 as MONEY),
    [Del av kundens totala exponering]	= CAST(0.00 as DECIMAL(16,2)),
    [Smittad exponering]				= CAST(0.00 as MONEY),
    [Antal dagar övertrasserad]		    = CASE 
											WHEN fk.Förfallodatum IS NULL THEN 0
											ELSE DATEDIFF(D,fk.Förfallodatum,@RapportPeriod)
										  END,
    [Steg]								= CASE
											WHEN fk.Förfallodatum IS NULL THEN 'Steg 1 0-29 dagar'
											WHEN DATEDIFF(D,fk.Förfallodatum,@RapportPeriod) < 30 THEN 'Steg 1 0-29 dagar' 
											WHEN DATEDIFF(D,fk.Förfallodatum,@RapportPeriod) BETWEEN 30 AND 89 THEN 'Steg 2 30-89 dagar'
											WHEN DATEDIFF(D,fk.Förfallodatum,@RapportPeriod) >= 90 THEN 'Steg 3 > 90 dagar'
										  END,
    [Reservering_Skuld]				    = CAST (0 as DECIMAL(16,2)),
    [Reservering_Ränta]				    = CAST (0 as DECIMAL(16,2)), 
    [Eftergift]							= '',
    [Beviljad kredit]				    = lan.BeviljatBelopp,
	[BeviljatDatum]						= lan.BeviljatDatum,
    [Ej utnyttjad kredit]			    = CASE 
											WHEN (lan.BeviljatBelopp - lan.AktuellSkuld) < 0.00 THEN 0
											ELSE lan.BeviljatBelopp - lan.AktuellSkuld
										  END,
    [Reservering för ej utnyttjad kredit] = CAST (0 as DECIMAL(16,2)),
    [Total reservering]					  = CAST (0 as DECIMAL(16,2)),
    [Ny kredit]							  = CASE 
												WHEN konto.DatumKontoStart > DATEADD(yy, DATEDIFF(yy, 0, @RapportPeriod), 0) THEN 1
												ELSE 0
											END
INTO #rapporttabell
FROM mart.dbo.v_FactLån_Månad lan
    INNER JOIN mart.dbo.v_DimLåneKonto konto on lan.LåneKontoID = konto.LåneKontoID
    INNER JOIN mart.dbo.v_DimKreditTyp kredtyp on lan.KreditTypID = kredtyp.KreditTypID 
    LEFT JOIN (
			 SELECT Kontonummer	  = konto.KontoNummer,
					Förfallodatum = MIN(krav.FörfalloDatum),
					Ränta		  = SUM(krav.RänteBelopp),
					AviBelopp	  = SUM(AmorteringBelopp + RänteBelopp + DröjsmålsBelopp + KorrigeratBelopp + AviBelopp + ÖvrigtBelopp) 
			 FROM mart.dbo.v_FactLån_Krav_Månad krav
				JOIN mart.dbo.v_DimKonto konto ON krav.KontoID = konto.KontoID
			 WHERE krav.Period = @RapportPeriod
				AND krav.AviLöpnummer <> 10
				AND krav.IDTyp = 'L'
				AND krav.Förfallodatum <= @RapportPeriod
			 GROUP BY konto.KontoNummer
			 ) fk on konto.LåneKontonummer = fk.Kontonummer AND fk.AviBelopp >= 180
    LEFT JOIN  (
				SELECT Kontonummer   = K.KontoNummer,
					  UpplupenRänta = F.UpplupenRäntaBelopp				
				FROM mart.dbo.v_FactLån_Kontrakt_Månad F
					INNER JOIN mart.dbo.v_DimKonto K ON F.KontoID = K.KontoID
				WHERE F.Period = @RapportPeriod
			 ) kontrakt on konto.LåneKontonummer = kontrakt.Kontonummer
WHERE lan.Period = @RapportPeriod
    AND kredtyp.KreditTyp in (
					     'Privatlån', --Privatlån
						 'HANDpenning',--ResterANDe ska grupperas som Övrigt
					     'Försäkringslån',
					     'Lån, övrigt',
					     'Billån',
					     'SkANDia omsatta bolån'
					    )
    AND lan.AktuellSkuld <> 0


--
-- Kontokrediter och kreditkort från övertrass

UNION ALL
SELECT
	[KontoID]							= FB.KontoID,
    [Typ av lån]					    = CASE 
										--WHEN Kontotyp = 'Allt i Ett-konto' AND Kreditlimit = 0 THEN 'Allt i Ett-konto utan kredit' --Inga konton utan krediter i denna rapporten.
											  WHEN Kontotyp = 'Allt i Ett-konto' AND Kreditlimit > 0 THEN 'Kontokredit'
											  WHEN Kontotyp = 'Betal & Kreditkort' THEN 'Betal & Kreditkort'
											  ELSE Kontotyp
										  END,
    [Kredittyp]							= '',
    [Kontonummer]					    = RIGHT(konto.KontoNummer,11),
    [Personnummer]					    = kund.Personnummer,
    [Skuld]								= abs(FB.Utnyttjad_kredit),
    [Ränta]								= 0,
    [Skuld+Ränta]					    = abs(FB.Utnyttjad_kredit), --(abs(FB.Utnyttjad_kredit) + fbm.SkuldRänta), Ränta läggs på krediten
    [Kundens totala exponering]		    = CAST(0.00 as MONEY),
    [Del av kundens totala exponering]	= CAST(0.00 as DECIMAL(16,2)),
    [Smittad exponering]				= CAST(0.00 as MONEY),
    [Antal dagar övertrasserad]		    = fb.Antal_dagar_som_övertrass,
    [Steg]								= CASE
											WHEN fb.Antal_dagar_som_övertrass < 30 
												OR abs(FB.Utnyttjad_kredit) < 180 THEN 'Steg 1 0-29 dagar' 
											WHEN fb.Antal_dagar_som_övertrass BETWEEN 30 AND 89 
												AND abs(FB.Utnyttjad_kredit) >= 180 THEN 'Steg 2 30-89 dagar'
											WHEN fb.Antal_dagar_som_övertrass >= 90 
												AND abs(FB.Utnyttjad_kredit) >= 180 THEN 'Steg 3 > 90 dagar'
										  END,								
    [Reservering_Skuld]				    = CAST (0 as DECIMAL(16,2)),
    [Reservering_Ränta]				    = CAST (0 as DECIMAL(16,2)), 							
    [Eftergift]							= '',	
    [Beviljad kredit]				    = fb.Kreditlimit,
	[BeviljatDatum]						= NULL,
    [Ej utnyttjad kredit]			    = CASE 
											WHEN (fb.Kreditlimit-abs(fb.Utnyttjad_kredit)) < 0.00 THEN 0
											ELSE fb.Kreditlimit-abs(fb.Utnyttjad_kredit)
										  END,	
    [Reservering för ej utnyttjad kredit]   = CAST (0 as DECIMAL(16,2)),	
    [Total reservering]						= CAST (0 as DECIMAL(16,2)),
    [Ny kredit]								= CASE 
												WHEN konto.DatumKontoStart > DATEADD(yy, DATEDIFF(yy, 0, @RapportPeriod), 0) THEN 1
												ELSE 0
											  END
FROM mart.dbo.FactBank_Övertrass_Månad FB
	JOIN mart.dbo.DimKonto konto
		ON FB.KontoID = konto.KontoID 
	JOIN mart.dbo.DimKund kund
		ON FB.KundID = kund.KundID
	LEFT JOIN (
			 SELECT 
				b.KontoNummer,
				a.SkuldRänta 
			 FROM mart.dbo.v_FactBank_Månad a 
				INNER JOIN mart.dbo.DimKonto b
				    ON a.KontoID = b.KontoID
			 WHERE a.Period = @RapportPeriod	  
			) fbm on konto.KontoNummer = fbm.KontoNummer
WHERE FB.Period = @RapportPeriod
	  AND FB.Kreditlimit > 0 --Inga konton utan krediter i denna rapporten.
	  --AND abs(FB.Utnyttjad_kredit) >= 180.00 --Beloppsgräns för att inte reservera små belopp i steg 2 och 3

UNION ALL

-- Kontokrediter och kreditkort som inte finns med i övertrass
SELECT
	[KontoID]							= fb.KontoID,
    [Typ av lån]					    = CASE 
											WHEN konto.Kontotyp = 'Allt i Ett-konto' THEN 'Kontokredit'
											WHEN konto.Kontotyp = 'Betal & Kreditkort' THEN 'Betal & Kreditkort'
											ELSE Kontotyp
										  END,
    [Kredittyp]							= '',
    [Kontonummer]					    = RIGHT(konto.KontoNummer,11),
    [Personnummer]					    = kund.Personnummer,
    [Skuld]								= abs(fb.UtnyttjadKredit),
    [Ränta]								= 0, --Ränta läggs på krediten
    [Skuld+Ränta]					    = abs(FB.UtnyttjadKredit),
    [Kundens totala exponering]		    = CAST(0.00 as MONEY),
    [Del av kundens totala exponering]	= CAST(0.00 as DECIMAL(16,2)),
    [Smittad exponering]				= CAST(0.00 as MONEY),
    [Antal dagar övertrasserad]		    = 0,
    [Steg]								= 'Steg 1 0-29 dagar',							
    [Reservering_Skuld]				    = CAST (0 as DECIMAL(16,2)),
    [Reservering_Ränta]				    = CAST (0 as DECIMAL(16,2)), 							
    [Eftergift]							= '',	
    [Beviljad kredit]				    = fb.BeviljadKredit,
	[BeviljatDatum]						= NULL,
    [Ej utnyttjad kredit]			    = CASE 
											WHEN (fb.BeviljadKredit-abs(fb.Utnyttjadkredit)) < 0.00 THEN 0
											ELSE fb.BeviljadKredit-abs(fb.Utnyttjadkredit)
										  END,	
    [Reservering för ej utnyttjad kredit]   = CAST (0 as DECIMAL(16,2)),	
    [Total reservering]						= CAST (0 as DECIMAL(16,2)),
    [Ny kredit]								= CASE 
												WHEN konto.DatumKontoStart > DATEADD(yy, DATEDIFF(yy, 0, @RapportPeriod), 0) THEN 1
												ELSE 0
											  END
FROM mart.dbo.v_FactBank_Månad fb
    INNER JOIN mart.dbo.v_DimKonto konto 
	   ON fb.KontoID = konto.KontoID
    INNER JOIN mart.dbo.v_DimKund kund 
	   ON fb.KundID = kund.KundID
WHERE fb.Period = @RapportPeriod
    AND fb.BeviljadKredit > 0
    AND konto.Kontotyp IN ('Allt i Ett-konto','Betal & Kreditkort')
    AND konto.KontoNummer NOT IN (
									SELECT b.KontoNummer 
									FROM mart.dbo.FactBank_Övertrass_Månad a
										INNER JOIN mart.dbo.v_DimKonto b on a.KontoID = b.KontoID
									WHERE Period = @RapportPeriod
								 )


--Lägg på reservering
UPDATE #rapporttabell 
   SET Reservering_Skuld = CASE 
								WHEN [Typ av lån] = 'Kontokredit'			AND [Steg] = 'Steg 1 0-29 dagar' THEN ( @UCRP_Kontokredit * @LGD * rt.Skuld)
								WHEN [Typ av lån] = 'Betal & Kreditkort'	AND [Steg] = 'Steg 1 0-29 dagar' THEN ( @UCRP_Kreditkort * @LGD * rt.Skuld)
								WHEN [Typ av lån] = 'Privatlån'				AND [Steg] = 'Steg 1 0-29 dagar' THEN ( @UCRP_Privatlan * @LGD * rt.Skuld)
								WHEN [Typ av lån] = 'Övrigt'				AND [Steg] = 'Steg 1 0-29 dagar' THEN ( @UCRP_Övrigt * @LGD * rt.Skuld)
								WHEN [Steg]		  = 'Steg 2 30-89 dagar'									 THEN (rt.Skuld * 0.5 * @LGD)
								WHEN [Antal dagar övertrasserad] BETWEEN 90 AND 179 AND [Steg] = 'Steg 3 > 90 dagar' THEN (rt.Skuld * 0.60)
								WHEN [Antal dagar övertrasserad] >= 180				AND [Steg] = 'Steg 3 > 90 dagar' THEN (rt.Skuld * 0.90)           
						   END,
	   Reservering_Ränta = CASE 
								
								WHEN [Antal dagar övertrasserad] BETWEEN 0 AND 89	THEN 0 --Endast reservering på ränta för Steg 3
								WHEN [Antal dagar övertrasserad] BETWEEN 90 AND 179 AND [Steg] = 'Steg 3 > 90 dagar' THEN (rt.Ränta * 0.60)
								WHEN [Antal dagar övertrasserad] >= 180				AND [Steg] = 'Steg 3 > 90 dagar' THEN (rt.Ränta * 0.90)           
						   END,
	   [Reservering för ej utnyttjad kredit] = CASE 
													WHEN [Typ av lån] = 'Kontokredit'			THEN ( @UCRP_Kontokredit * @LGD * (rt.[Ej utnyttjad kredit] * @CCF_Kontokredit))
													WHEN [Typ av lån] = 'Betal & Kreditkort'	THEN ( @UCRP_Kreditkort * @LGD * (rt.[Ej utnyttjad kredit] * @CCF_Kreditkort))
													WHEN [Typ av lån] = 'Privatlån'				THEN ( @UCRP_Privatlan * @LGD * (rt.[Ej utnyttjad kredit] * @CCF_Privatlan))
													WHEN [Typ av lån] = 'Övrigt'				THEN ( @UCRP_Övrigt * @LGD * (rt.[Ej utnyttjad kredit] * @CCF_Övrigt))
											   END
	FROM #rapporttabell rt

/*
UPDATE #rapporttabell
    SET
	   [Total reservering] = (rt.Reservering_Skuld + Reservering_Ränta + rt.[Reservering för ej utnyttjad kredit])
    FROM #rapporttabell rt
*/

--De problemlån (steg 3) som identifierats och som överstiger 20% av kundens totala exponering. Den totala exponeringen skall i så fall räknas med (mellanskillnaden). 
UPDATE #rapporttabell 
    SET
	  [Kundens totala exponering]		  = a.Utlåning,
	  [Del av kundens totala exponering] = CAST((skuld/a.utlåning) as DECIMAL(16,6)),
	  [Smittad exponering]			  = CASE 
											WHEN (#rapporttabell.skuld/a.utlåning) > 0.20 AND [Antal dagar övertrasserad] >= 90 THEN a.utlåning - Skuld
											ELSE 0
										END	  
    FROM (
		  SELECT 
		  	    Personnummer = k.Personnummer, 
		  	    Utlåning     = SUM(f.Utlåning)
		  FROM mart.dbo.v_FactUtlåning f 
			JOIN mart.dbo.DimKund k on f.KundID = k.KundID
		  WHERE period = @Period
		  GROUP BY k.Personnummer
	     ) a 
	WHERE #rapporttabell.personnummer = a.Personnummer
		--AND [Antal dagar övertrasserad] >= 90 


SELECT 
	  [KontoID],
	  [Typ av lån],
	  [Kredittyp],
	  [Kontonummer],
	  [Personnummer],
	  [Skuld],
	  [Ränta],
	  [Skuld+Ränta],
	  [Kundens totala exponering],
	  [Del av kundens totala exponering],
	  [Smittad exponering],
	  [Antal dagar övertrasserad],
	  [Steg],							
	  [Reservering_Skuld],
	  [Reservering_Ränta],							
	  [Eftergift],
	  [Beviljad kredit],
	  [BeviljatDatum],
	  [Ej utnyttjad kredit],
	  [Reservering för ej utnyttjad kredit],
	  [Total reservering] = isnull([Reservering_Skuld],0) + isnull([Reservering_Ränta],0) + isnull([Reservering för ej utnyttjad kredit],0),
	  [Ny kredit]
FROM #rapporttabell












GO


